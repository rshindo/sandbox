apply plugin: 'java'
apply plugin: 'war'
project.webAppDirName = 'WebContent'


sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// For Doma
processResources.destinationDir = compileJava.destinationDir
compileJava.dependsOn processResources

// NetBeans will automatically add "run" and "debug" tasks relying on the
// "mainClass" property. You may however define the property prior executing
// tasks by passing a "-PmainClass=<QUALIFIED_CLASS_NAME>" argument.
//
// Note however, that you may define your own "run" and "debug" task if you
// prefer. In this case NetBeans will not add these tasks but you may rely on
// your own implementation.
if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

repositories {
    mavenCentral()
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

dependencies {
    
    compile 'javax:javaee-api:7.0'
    
    // Doma Framework
    compile "org.seasar.doma:doma:2.6.0"
    
    // SQL Server JDBC Driver
    compile fileTree(dir: 'lib', include: ['*.jar'])
    
    // Jersey
    compile 'org.glassfish.jersey.media:jersey-media-multipart:2.22.1'
    compile 'org.glassfish.jersey.core:jersey-server:2.22.1'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.22.1'
    
    // Hibernate Validator
    compile 'org.hibernate:hibernate-validator:5.2.2.Final'

    // Jersey Bean Validation
    compile 'org.glassfish.jersey.ext:jersey-bean-validation:2.22.1'
    
    // TODO: Add dependencies here ...
    // You can read more about how to add dependency here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:how_to_declare_your_dependencies
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    
    // Jersey Test Framework
    testCompile 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.22.1'
//    testCompile 'org.glassfish:jsonp-jaxrs:1.0'
    testCompile 'com.fasterxml.jackson.core:jackson-databind:2.3.4'

    // HK2
    testCompile 'org.glassfish.hk2:hk2-api:2.3.0'

}

war {
    baseName = 'JerseyTest'
}

ext {
    glassFishHome = 'C:\\glassfish-4.1'
    asadmin = glassFishHome + (isWindows() ? '\\bin\\asadmin.bat' : '/bin/asadmin')
    domain = 'domain1'
}

def isWindows() {
    return System.properties['os.name'].toLowerCase().contains('windows')
}

task startServer(type:Exec) {
    doFirst {
        commandLine asadmin, 'start-domain', '--debug=true', domain
    }
}

task stopServer(type:Exec) {
    commandLine asadmin, 'stop-domain', domain
}

task deploy(type:Exec) {
    doFirst {
        commandLine asadmin, 'deploy', '--force=true', war.archivePath
        
    }
}